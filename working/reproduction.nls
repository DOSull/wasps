
;; The MIT License (MIT)
;;
;; Copyright (c) 2020 David O'Sullivan
;;
;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without restriction,
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to  permit persons to whom the Software is furnished to do so,
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be included
;; in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
;; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.

to-report get-next-generation [lambda local-pops]
  if lambda = 0 [
    report (list 0 0 0)
  ]
  ;; Going to assume THREE populations
  ;; only wild and GM reproduce
  let reproductive sum but-last local-pops
  ;; from which we get the new-population
  let new-pop random-poisson (lambda * reproductive)
  ;; the proportions of these in each of wild, GM and sterile
  ;; determined by w^2, 2gw, g^2
  let pop-rel binom-series but-last local-pops
  report repeat-binomial-draws new-pop pop-rel
end

;; report binomial coefficients [x_0^2 2x-0x_1 x_1^2]
;; from an input list [x0 x1]
to-report binom-series [x]
  let x0 item 0 x
  let x1 item 1 x
  report (list (x0 * x0) (2 * x0 * x1) (x1 * x1))
end

;to-report get-n-binom-p [n p]
;  let result [0 0]
;  repeat n [
;    ifelse random-float 1 < p
;    [ set result replace-item 0 result (item 0 result + 1) ]
;    [ set result replace-item 1 result (item 1 result + 1) ]
;  ]
;  report result
;end


; This binomial algorithm from
; Devroye. L. 1960. Generating the maximum of independent identically
; distributed random variables. Computers and Mathematics with
; Applications 6, 305-315.
; should be a bit quicker because it only needs ~ np random-float calls
; Based on code from https://stackoverflow.com/questions/23561551/a-efficient-binomial-random-number-generator-code-in-java#23574723
to-report get-binomial-2 [n p]
  ; need to trap p = 0 and p = 1
  if p = 1 [ report n ]
  if p = 0 [ report 0 ]
  let ln-q ln (1 - p)
  let x 0
  let s 0
  ; also need to avoid x = n
  while [x < n] [
    set s s + ln (random-float 1) / (n - x)
    if s < ln-q [
      report x
    ]
    set x x + 1
  ]
  report x
end

;; perform repeated binomial draws from population n
;; without replacement, with p based on ratios determined 
;; from the list of values in rel-nums
to-report repeat-binomial-draws [n rel-nums]
  ;; empty list for the results
  let result []
  ;; remember how many values we will need
  let num-values length rel-nums
  ;; keep going as long as we haven't drawn all n
  while [sum result < n] [
    ;; the next draw is based on the proportion of 
    ;; the remaining total represented by the first number
    ;; first time around this will be n0 / total
    ;; then it will be n1 / (total - n0), and so on
    let px first rel-nums / sum rel-nums
    ;; meanwhile the number remaining to be drawn is n - sum result, so...
    set result sentence result get-binomial-2 (n - sum result) px
    ;; remove the first item from the list of relative numbers
    set rel-nums but-first rel-nums
  ]
  ;; if the result is not the required length then pack it with zeros
  ifelse length result < num-values
  [ report pack-zeros result num-values]
  [ report result ]
end


;; naive binomial generator (not used... it's slow!)
to-report get-binomial [n p]
  report sum n-values n [i -> ifelse-value (random-float 1 < p) [1] [0]]
end


;; pack list out to length 0 with zeros
to-report pack-zeros [lst n]
  report sublist (sentence lst n-values num-pops [x -> 0]) 0 n
end


to-report patch-at-offset [xy]
  report patch-at item 0 xy item 1 xy
end
