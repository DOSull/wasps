;; The MIT License (MIT)
;;
;; Copyright (c) 2020 David O'Sullivan
;;
;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without restriction,
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to  permit persons to whom the Software is furnished to do so,
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be included
;; in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
;; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.

to go
  ifelse total-pop > 0 [
    ;; model-wide mean R this year
    set R-mean birth-rate - mortality
    set R-annual random-normal R-mean pop-sd
    ;; habitable patches might have population
    ask the-habitable-land [
      if pop > 0 [
        ;; get the next generation and disperse it
        disperse next-generation
      ]
    ]
    ;; if any regular wasp releases are requested do that now
    if periodicity > 0 and total-wild > 0 [
      if ticks mod periodicity = 0 [
        release-wasps false
      ]
    ]
  ]
  [ stop ]
  ;; update the numbers and the display and plots
  update-populations
  colour-patches
  renew-plots
  tick
  if debug? and ticks mod 10 = 0 [ show (word ticks ": " timer) ]
end

;; this is basic housekeeping on the various totals
to update-populations
  ask the-habitable-land [
    set pops next-pops
    set pop sum pops
    set next-pops n-values num-subpops [ i -> 0 ]
  ]
  set total-pop sum [pop] of the-habitable-land
  set total-wild sum [item 0 pops] of the-habitable-land
  set total-extent count the-habitable-land with [pop > 0]
  set prop-occupied total-extent / count the-land
  set mean-occupancy-rate mean [pop / capacity] of the-habitable-land

  if monitor-area? [
    ask monitoring-area [
      set history lput (fput ticks pops) history
    ]
  ]
end



to-report increment-list-item [lst i x]
  let y item i lst
  report replace-item i lst (x + y)
end

to reset-map
  reset-ticks
  clear-all-plots
  ask patches [
    set pops init-pops
    set pop init-pop
  ]
  set total-pop sum [pop] of patches
  colour-patches
  renew-plots
end

to save-monitor
  let fname (word "monitor-dmean" d-mean "-pldd" p-ldd "-rmean" R-mean ".txt")
  if file-exists? fname [
    file-delete fname
  ]
  file-open fname
  file-print "x y t k p1 p2 p3"
  let nrows length [history] of one-of monitoring-area
  let i 0
  repeat nrows [
    ask monitoring-area [
      let data item i history
      file-type word pxcor " "
      file-type word pycor " "
      file-type word item 0 data " "     ;; ticks
      file-type word capacity " " 
      file-type word item 1 data " "     ;; wild
      file-type word item 2 data " "     ;; GM
      file-print item 3 data             ;; sterile
    ]
    set i i + 1
  ]
  file-close
end
